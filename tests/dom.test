# Features covered: DOM functions
#
# This file contains a collection of tests for the domNode command methods.
#
#    dom-1.*:  domNode command syntax
#    dom-3.*:  documentElement
#    dom-4.*:  setAttributeNS
#    dom-6.*:  appendChild
#    dom-7.*:  getElementsByTagName
#    dom-8.*:  getElementsByTagNameNS
#    dom-9.*:  nodeValue  
#    dom-10.*: setAttribute, again setAttributeNS
#    dom-11.*: disableOutputEscaping 
#    dom-12.*: cloneNode
#    dom-13.*: appendFromScript
#    dom-14.*: appendFromList  
#    dom-15.*: delete
#    dom-16.*: getAttribute
#    dom-17.*: nodeType
#    dom-18.*: attributes
#    dom-19.*: removeAttribute, removeAttributeNS
#    dom-20.*: parentNode
#    dom-21.*: hasChildNodes
#    dom-22.*: localName, prefix
#    dom-23.*: replaceChild
#    dom-24.*: getLine, getColumn
#    dom-25.*: hasAttribute, hasAttributeNS
#    dom-26.*: appendXML
#    dom-27.*: target, data
#    dom-28.*: getAttributeNS
#    dom-29.*: ownerDocument 
#    dom-999.* Misc Tests 
#
# Copyright (c) 2002 Rolf Ade.
#
# RCS: @(#) $Id$

source [file join [file dir [info script]] loadtdom.tcl]

test dom-1.1 {to less arguments to domNode command} {
    set doc [dom createDocument "root"]
    set root [$doc documentElement]
    set result [catch {$root}]
    $doc delete
    set result
} {1}

test dom-1.2 {to less arguments to domNode command} {
    set doc [dom createDocument "root"]
    set root [$doc documentElement]
    set result [catch {domNode $root}]
    $doc delete
    set result
} {1}

test dom-1.3 {to less arguments to domNode command} {
    catch {domNode}
} {1}

test dom-3.1 {repetitived documentElement with objVar, then delete} {knownBug} {
    dom createDocument "root" doc 
    $doc documentElement root
    $doc delete
    
    dom createDocument "\u00c4\u00d4\u00dc" doc 
    $doc documentElement root
    set result [$root nodeName]
    $doc delete
    set result
} "\u00c4\u00d4\u00dc"

#  test dom-3.2 {repetitived documentElement, then delete} {
#      set doc [dom createDocument "root"] 
#      $doc documentElement root
#      $doc delete
    
#      set doc [dom createDocument "\u00c4\u00d4\u00dc"]
#      $doc documentElement root
#      set result [$root nodeName]
#      $doc delete
#      set result
#  } "\u00c4\u00d4\u00dc"

test dom-3.3 {repetitived documentElement, then delete} {
    set doc [dom createDocument "root"] 
    set root [$doc documentElement]
    $doc delete
    
    set doc [dom createDocument "\u00c4\u00d4\u00dc"]
    set root [$doc documentElement]
    set result [$root nodeName]
    $doc delete
    set result
} "\u00c4\u00d4\u00dc"

test dom-4.1 {create nodes with same prefix, different uri's} {
    dom createDocumentNS "uri1" "p:a" doc
    set root [$doc documentElement]
    set node1 [$doc createElementNS "uri2" "p:b"]
    $root appendChild $node1
    set node2 [$doc createElementNS "uri1" "p:c"]
    $node1 appendChild $node2
    set result [$root asXML]
    $doc delete
    set result
} {<p:a xmlns:p="uri1">
    <p:b xmlns:p="uri2">
        <p:c xmlns:p="uri1"/>
    </p:b>
</p:a>
}

test dom-4.3 {setAttribute} {
    dom createDocumentNS "uri1" "p:root" doc
    set root [$doc documentElement]
    $root setAttribute attr1 attr1Value
    set result [$root asXML]
    $doc delete
    set result
} {<p:root xmlns:p="uri1" attr1="attr1Value"/>
}

test dom-4.5 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS ""  xmlns:p uri
    $root setAttributeNS uri p:attr attrValue
    set result [$root asXML]
    $doc delete
    set result
} {<root xmlns:p="uri" p:attr="attrValue"/>
}

test dom-4.6 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS uri p:attr attrValue
    $root setAttributeNS ""  xmlns:p uri
    set result [$root asXML]
    $doc delete
    set result
} {<root xmlns:p="uri" p:attr="attrValue"/>
}

test dom-4.7 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS uri p:attr1 attrValue
    set result [$root getAttributeNS uri attr1]
    $doc delete
    set result
} {attrValue}

test dom-4.8 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS ""  xmlns:p uri
    $root setAttributeNS uri p:attr1 attrValue
    set result [$root getAttributeNS uri attr1]
    $doc delete
    set result
} {attrValue}

test dom-4.9 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS uri p:attr1 attrValue
    set result [$root attributes *]
    $doc delete
    set result
} {{attr1 p uri}}

test dom-4.10 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    set result [catch {$root setAttributeNS {} p:attr1 attrValue}]
    $doc delete
    set result
} {1}

test dom-4.11 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    set result [catch {$root setAttributeNS uri attr1 attrValue}]
    $doc delete
    set result
} {1}

test dom-4.12 {setAttributeNS - special prefix "xml"} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS "" xml:attr1 attrValue
    set result [$root attributes *]
    $doc delete
    set result
} {{attr1 xml http://www.w3.org/XML/1998/namespace}}

test dom-4.13 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS uri p:attr1 attrValue
    $root setAttributeNS uri o:attr1 newValue
    set result [$root attributes *]
    $doc delete
    set result
} {{attr1 p uri}}

test dom-4.14 {setAttributeNS} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS uri p:attr1 attrValue
    $root setAttributeNS uri o:attr1 newValue
    set result [$root getAttributeNS uri attr1]
    $doc delete
    set result
} {newValue}

test dom-4.15 {setAttributeNS - use as setAttribute} {
    dom createDocument "root" doc
    set root [$doc documentElement]
    $root setAttributeNS "" attr1 attrValue
    set result [$root attributes *]
    $doc delete
    set result
} {attr1}

test dom-4.16 {setAttributeNS - set multiple Attributes at once} {
    set doc [dom createDocumentNS uri1 "p1:root"]
    set root [$doc documentElement]
    $root setAttributeNS "" xmlns:p2 uri2
    $root setAttributeNS uri1 p1:a1 1 uri1 p1:a2 2 uri2 p2:a3 3 "" a4 4
    set result [$root asXML]
    $doc delete
    set result
} {<p1:root xmlns:p1="uri1" xmlns:p2="uri2" p1:a1="1" p1:a2="2" p2:a3="3" a4="4"/>
}

test dom-6.1 {insert FQ Element} {
    set doc [dom parse {<root xmlns:p="uri1"/>}]
    set root [$doc documentElement]
    set newNode [$doc createElementNS uri2 p:foo]
    $root appendChild $newNode
    set result [$root asXML]
    $doc delete
    set result
} {<root xmlns:p="uri1">
    <p:foo xmlns:p="uri2"/>
</root>
}

test dom-6.2 {appendChild} {
    set doc [dom createDocument XMI]
    set root [$doc documentElement]
    set A1tag [$doc createElement "A1"]
    set A2subtag [$doc createElement "A2sub"]
    set A2tag [$doc createElement "A2"]
    $A2tag appendChild $A2subtag

    set Atag [$doc createElement "A"]
    $Atag appendChild $A1tag
    $Atag appendChild $A2tag

    set Btag [$doc createElement "B"]
    $Btag appendChild $Atag

    set Ctag [$doc createElement "C"]
    set result 0
    if {$root == "[$doc documentElement]"} {set result 1}
    $doc delete
    set result
} {1}

set doc [dom parse {<root><foobar/><barfoo/><foobaz/><a/><b/><a/><c/></root>}]
set root [$doc documentElement]

test dom-7.1 {getElementsByTagName - doc method} {
    llength [$doc getElementsByTagName a]
} {2}

test dom-7.2 {getElementsByTagName - doc method} {
    llength [$doc getElementsByTagName c]
} {1}

test dom-7.3 {getElementsByTagName - doc method} {
    llength [$doc getElementsByTagName foo]
} {0}

test dom-7.4 {getElementsByTagName - node method} {
    llength [$root getElementsByTagName a]
} {2}

test dom-7.5 {getElementsByTagName - node method} {
    llength [$root getElementsByTagName c]
} {1}

test dom-7.6 {getElementsByTagName - node method} {
    llength [$root getElementsByTagName foo]
} {0}

test dom-7.7 {getElementsByTagName - node method '*' wildcard} {
    llength [$root getElementsByTagName *]
} {7}

test dom-7.8 {getElementsByTagName - doc method '*' wildcard} {
    llength [$doc getElementsByTagName *]
} {8}

test dom-7.9 {getElementsByTagName - node method tcl glob style} {
    llength [$root getElementsByTagName foo*]
} {2}

test dom-7.9 {getElementsByTagName - node method tcl glob style} {
    llength [$root getElementsByTagName *oo*]
} {3}

test dom-7.10 {getElementsByTagName - doc method tcl glob style} {
    llength [$doc getElementsByTagName foo*]
} {2}

test dom-7.11 {getElementsByTagName - doc method tcl glob style} {
    llength [$doc getElementsByTagName *oo*]
} {4}

test dom-7.12 {getElementsByTagName - doc method empty result} {
    $doc getElementsByTagName noSuchANodeName
} {}

test dom-7.13 {getElementsByTagName - node method empty result} {
    $root getElementsByTagName noSuchANodeName
} {}

test dom-7.14 {getElementsByTagName - doc method: doc order of result} {
    set nodes [$doc getElementsByTagName *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {root foobar barfoo foobaz a b a c }

test dom-7.15 {getElementsByTagName - node method: doc order of result} {
    set nodes [$root getElementsByTagName *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {foobar barfoo foobaz a b a c }

$doc delete

set doc [dom parse {
<root>mixed content 
   <elem><b>important</b> more content <b>again important</b></elem>
   <!-- a comment --> 
   <p:elem xmlns:p="aNS"><b/></p:elem>
</root>}]
set root [$doc documentElement]

test dom-7.16 {getElementsByTagName - node method mixed content} {
    llength [$doc getElementsByTagName elem]
} {1}

test dom-7.17 {getElementsByTagName - node method mixed content} {
    llength [$root getElementsByTagName elem]
} {1}

test dom-7.18 {getElementsByTagName - doc method mixed content} {
    llength [$doc getElementsByTagName b]
} {3}

test dom-7.19 {getElementsByTagName - node method mixed content} {
    llength [$root getElementsByTagName b]
} {3}

test dom-7.20 {getElementsByTagName - not a element node} {
    set textnode [$root selectNodes {descendant::text()[1]}]
    catch {$textnode getElementsByTagName b} errMsg
    set errMsg
} {Node must be an element node.}

$doc delete 

set doc [dom parse {<?xml version="1.0"?>
<soap:Envelope
xmlns:soap="http://www.w3.org/2001/12/soap-envelope"
soap:encodingStyle="http://www.w3.org/2001/12/soap-encoding">
  <soap:Body xmlns:m="http://www.stock.org/stock">
    <m:GetStockPrice>
      <m:StockName>IBM</m:StockName>
    </m:GetStockPrice>
  </soap:Body>
</soap:Envelope>}]
set root [$doc documentElement]

test dom-8.1 {getElementsByTagNameNS - root method} {
    [$root getElementsByTagNameNS "http://www.stock.org/stock" GetStockPrice] nodeName
} {m:GetStockPrice}

test dom-8.2 {getElementsByTagNameNS - root method} {
    [$root getElementsByTagNameNS "*" GetStockPrice] nodeName
} {m:GetStockPrice}

test dom-8.3 {getElementsByTagNameNS - root method} {
    llength [$root getElementsByTagNameNS "http://www.stock.org/stock" *]
} {2}

test dom-8.4 {getElementsByTagNameNS - doc method} {
    [$doc getElementsByTagNameNS "http://www.stock.org/stock" GetStockPrice] nodeName
} {m:GetStockPrice}

test dom-8.5 {getElementsByTagNameNS - doc method} {
    [$doc getElementsByTagNameNS "*" GetStockPrice] nodeName
} {m:GetStockPrice}

test dom-8.6 {getElementsByTagNameNS - doc method} {
    llength [$doc getElementsByTagNameNS "http://www.stock.org/stock" *]
} {2}

$doc delete 

set doc [dom parse {
<root>mixed content 
   <elem><b>important</b> more content <b>again important</b></elem>
   <!-- a comment --> 
   <elem xmlns="NS1"><b/></elem>
   <elem xmlns="NS2"/>
   <p:pathologic xmlns:p="firstp">
     <p:pathologic xmlns:p="secondp">
       <p:pathologic xmlns:p="firstp"/>
     </p:pathologic>
   </p:pathologic>
</root>}]
set root [$doc documentElement]

test dom-8.7 {getElementsByTagNameNS - doc method pathologic XML} {
    llength [$doc getElementsByTagNameNS "firstp" pathologic]
} {2}

test dom-8.8 {getElementsByTagNameNS - doc method pathologic XML} {
    llength [$doc getElementsByTagNameNS "secondp" pathologic]
} {1}

test dom-8.9 {getElementsByTagNameNS - doc method pathologic XML} {
    llength [$doc getElementsByTagNameNS "*" pathologic]
} {3}

test dom-8.10 {getElementsByTagNameNS - node method pathologic XML} {
    llength [$root getElementsByTagNameNS "firstp" pathologic]
} {2}

test dom-8.11 {getElementsByTagNameNS - node method pathologic XML} {
    llength [$root getElementsByTagNameNS "secondp" pathologic]
} {1}

test dom-8.12 {getElementsByTagNameNS - node method pathologic XML} {
    llength [$root getElementsByTagNameNS "*" pathologic]
} {3}

test dom-8.13 {getElementsByTagNameNS - doc method} {
    llength [$doc getElementsByTagNameNS "NS1" elem]
} {1}

test dom-8.14 {getElementsByTagNameNS - doc method} {
    llength [$doc getElementsByTagNameNS "NS2" elem]
} {1}

test dom-8.15 {getElementsByTagNameNS - doc method} {
    llength [$doc getElementsByTagNameNS "*" elem]
} {3}

test dom-8.16 {getElementsByTagNameNS - node method} {
    llength [$root getElementsByTagNameNS "NS1" elem]
} {1}

test dom-8.17 {getElementsByTagNameNS - node method} {
    llength [$root getElementsByTagNameNS "NS2" elem]
} {1}

test dom-8.18 {getElementsByTagNameNS - node method} {
    llength [$root getElementsByTagNameNS "*" elem]
} {3}

test dom-8.19 {getElementsByTagNameNS - doc method empty namespace} {
    set nodes [$doc getElementsByTagNameNS "" *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {root elem b b }

test dom-8.19 {getElementsByTagNameNS - node method empty namespace} {
    set nodes [$root getElementsByTagNameNS "" *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {elem b b }

test dom-8.20 {getElementsByTagNameNS - not a element node} {
    set textnode [$root selectNodes {descendant::text()[1]}]
    catch {$textnode getElementsByTagName b} errMsg
    set errMsg
} {Node must be an element node.}

$doc delete

set doc [dom parse {
<root xmlns="defaultNS1">
  <elem1 xmlns="">
    <elem2 xmlns="defaultNS2">
      <elem3 xmlns=""/>
    </elem2>
  </elem1>
</root>}]
set root [$doc documentElement]

test dom-8.21 {getElementsByTagNameNS - unset default NS} {
    set nodes [$root getElementsByTagNameNS "" *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {elem1 elem3 }
   
test dom-8.22 {getElementsByTagName - unset default NS} {
    set nodes [$root getElementsByTagName *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {elem1 elem2 elem3 }

test dom-8.23 {getElementsByTagNameNS - unset default NS} {
    set nodes [$root getElementsByTagNameNS * *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {elem1 elem2 elem3 }

test dom-8.24 {getElementsByTagNameNS - unset default NS} {
    set nodes [$doc getElementsByTagNameNS * *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {root elem1 elem2 elem3 }

test dom-8.25 {getElementsByTagNameNS - unset default NS} {
    set nodes [$root getElementsByTagNameNS "" *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {elem1 elem3 }

test dom-8.26 {getElementsByTagNameNS - unset default NS} {
    set nodes [$doc getElementsByTagNameNS "" *]
    set result ""
    foreach node $nodes {
        append result "[$node nodeName] "
    }
    set result
} {elem1 elem3 }

$doc delete

set doc [dom parse {
<root>text node<!--comment node--><?mytarget PI node?></root>}]
set root [$doc documentElement]

test dom-9.1 {nodeValue - TEXT_NODE} {
    [$root firstChild] nodeValue
} {text node}

test dom-9.2 {nodeValue - COMMENT_NODE} {
    set firstChild [$root firstChild]
    set commentNode [$firstChild nextSibling]
    $commentNode nodeValue
} {comment node}

test dom-9.3 {nodeValue - PROCESSING_INSTRUCTION_NODE} {
    [$root lastChild] nodeValue
} {PI node}

test dom-9.4 {nodeValue - CDATA_SECTION_NODE} {
    set cdNode [$doc createCDATASection "cdata section node"]
    $root appendChild $cdNode
    [$root lastChild] nodeValue
} {cdata section node}

test dom-9.5 {nodeValue with set - TEXT_NODE} {
    set result [[$root firstChild] nodeValue "new text value"]
    append result "/" [[$root firstChild] nodeValue]
} {text node/new text value}

test dom-9.6 {nodeValue with set - COMMENT_NODE} {
    set textNode [$root firstChild]
    set node [$textNode nextSibling]
    set result [$node nodeValue  "new comment text"]
    append result "/" [$commentNode nodeValue]
} {comment node/new comment text}

test dom-9.7 {nodeValue - PROCESSING_INSTRUCTION_NODE does not allow setting} {
    set piNode [$root selectNodes processing-instruction('mytarget')]
    catch {$piNode nodeValue "new pi value"}
} {1}

test dom-9.8 {nodeValue - CDATA_SECTION_NODE} {
    set result [[$root lastChild] nodeValue "new text"]
    append result "/" [[$root lastChild] nodeValue]
} {cdata section node/new text}

$doc delete

set doc [dom parse <foo/>]
set root [$doc documentElement]

test dom-10.1 {setAttribute - set multiple attributes at once} {
    $root setAttribute a1 1 a2 2 a3 3 a4 4 a5 5 a6 6 a7 7 a8 8
    $root asXML -indent none
} {<foo a1="1" a2="2" a3="3" a4="4" a5="5" a6="6" a7="7" a8="8"/>}

$doc delete

set doc [dom parse {<root>&amp;</root>}]
set root [$doc documentElement]
set textnode [$root firstChild]

test dom-11.1 {disableOutputEscaping} {
    $textnode disableOutputEscaping
} {0}

test dom-11.2 {disableOutputEscaping} {
    $textnode disableOutputEscaping 1
} {0}

test dom-11.3 {disableOutputEscaping} {
    $textnode disableOutputEscaping
} {1}

test dom-11.4 {disableOutputEscaping} {
    $root asXML -indent none
} {<root>&</root>}

$doc delete

test dom-11.5 {disableOutputEscaping} {
    set doc [dom createDocument root]
    set textnode [$doc createTextNode "<p>some <b>important</b> text</p>"]
    $textnode disableOutputEscaping 1
    set root [$doc documentElement]
    $root appendChild $textnode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><p>some <b>important</b> text</p></root>}

test dom-11.6 {disableOutputEscaping} {
    set doc [dom createDocument root]
    set textnode [$doc createTextNode "<p>some <b>important</b> text</p>"]
    $textnode disableOutputEscaping 1
    set root [$doc documentElement]
    $root appendChild $textnode
    set textnode [$doc createTextNode "&"]
    $root appendChild $textnode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><p>some <b>important</b> text</p>&amp;</root>}

test dom-11.6 {disableOutputEscaping} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    set textnode [$doc createTextNode "&"]
    $root appendChild $textnode
    set textnode [$doc createTextNode "<p>some <b>important</b> text</p>"]
    $textnode disableOutputEscaping 1
    $root appendChild $textnode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root>&amp;<p>some <b>important</b> text</p></root>}

test dom-12.1 {cloneNode} {
    set doc [dom parse {<root/>}]
    set root [$doc documentElement]
    set newNode [$root cloneNode]
    $root appendChild $newNode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><root/></root>}

test dom-12.2 {cloneNode} {
    set doc [dom parse {<root><e/>text<!--comment--><?pi foo?></root>}]
    set root [$doc documentElement]
    set newNode [$root cloneNode]
    $root appendChild $newNode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e/>text<!--comment--><?pi foo?><root/></root>}

test dom-12.3 {cloneNode} {
    set doc [dom parse {<root><e/>text<!--comment--><?pi foo?></root>}]
    set root [$doc documentElement]
    set newNode [[$root firstChild] cloneNode]
    $root appendChild $newNode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e/>text<!--comment--><?pi foo?><e/></root>}

test dom-12.4 {cloneNode} {
    set doc [dom parse {<root><e/>text<!--comment--><?pi foo?></root>}]
    set root [$doc documentElement]
    set newNode [[$root selectNodes {node()[2]}] cloneNode]
    $root appendChild $newNode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e/>text<!--comment--><?pi foo?>text</root>}

test dom-12.5 {cloneNode} {
    set doc [dom parse {<root><e/>text<!--comment--><?pi foo?></root>}]
    set root [$doc documentElement]
    set newNode [[$root selectNodes {node()[3]}] cloneNode]
    $root appendChild $newNode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e/>text<!--comment--><?pi foo?><!--comment--></root>}

test dom-12.5 {cloneNode} {
    set doc [dom parse {<root><e/>text<!--comment--><?pi foo?></root>}]
    set root [$doc documentElement]
    set newNode [[$root lastChild] cloneNode]
    $root appendChild $newNode
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e/>text<!--comment--><?pi foo?><?pi foo?></root>}

test dom-12.6 {cloneNode -deep} {
    set doc [dom parse {<r><e><a/>text<b><c/></b></e></r>}]
    set root [$doc documentElement]
    set result [[[$root firstChild] cloneNode -deep] asXML -indent none]
    $doc delete
    set result
} {<e><a/>text<b><c/></b></e>}

test dom-12.7 {cloneNode -deep} {
    set doc [dom parse {<r><e><a/>text<b><c/></b></e></r>}]
    set root [$doc documentElement]
    $root appendChild [[$root firstChild] cloneNode -deep]
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<r><e><a/>text<b><c/></b></e><e><a/>text<b><c/></b></e></r>}

test dom-12.8 {cloneNode -deep} {
    set doc [dom parse {<r><z/><e><a/>text<b><c/></b></e></r>}]
    set root [$doc documentElement]
    set removedNode [$root removeChild [$root firstChild]]
    $root appendChild [[$root firstChild] cloneNode -deep]
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<r><e><a/>text<b><c/></b></e><e><a/>text<b><c/></b></e></r>}

test dom-12.9 {cloneNode -deep} {
    set doc [dom parse {<r><z/><e><a/>text<b><c/></b></e></r>}]
    set root [$doc documentElement]
    set removedNode [$root removeChild [$root firstChild]]
    $root appendChild [[$root firstChild] cloneNode -deep]
    unset result
    lappend result [$removedNode nextSibling]
    lappend result [$removedNode previousSibling]
    $doc delete
    set result
} {{} {}}

test dom-12.10 {cloneNode -deep} {
    set doc [dom parse {<r><y/><z/><e><a/>text<b><c/></b></e></r>}]
    set root [$doc documentElement]
    $root removeChild [$root firstChild]
    set removedNode [$root removeChild [$root firstChild]]
    $root appendChild [[$root firstChild] cloneNode -deep]
    unset result
    lappend result [[$removedNode nextSibling] nodeName]
    lappend result [$removedNode previousSibling]
    $doc delete
    set result
} {y {}}

namespace eval nodeCmds {
    dom createNodeCmd elementNode e1
    dom createNodeCmd elementNode e2
    dom createNodeCmd commentNode c
    dom createNodeCmd textNode    t
    dom createNodeCmd cdataNode   cdata
    dom createNodeCmd piNode      pi
    dom createNodeCmd parserNode  parser
}

test dom-13.1 {appendFromScript - elementNode} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    $root appendFromScript nodeCmds::e1
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1/></root>}

test dom-13.2 {appendFromScript - elementNode} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    namespace eval nodeCmds {
        $root appendFromScript {
            e1
            e2
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1/><e2/></root>}

test dom-13.3 {appendFromScript - elementNode} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    namespace eval nodeCmds {
        $root appendFromScript {
            e1 {
                e2 {
                    e1
                }
            }
            e2
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1><e2><e1/></e2></e1><e2/></root>}

test dom-13.4 {appendFromScript - elementNode with attributes as options} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    namespace eval nodeCmds {
        $root appendFromScript {
            e1 -attr1 attr1Value -attr2 "attr 2 Value"
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1 attr1="attr1Value" attr2="attr 2 Value"/></root>}

test dom-13.5 {appendFromScript - elementNode with attributes as list} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    set attlist [list -a1 "some & value" -a2 "another attvalue"]
    namespace eval nodeCmds {
        $root appendFromScript {
            e1 $attlist {}
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1 a1="some &amp; value" a2="another attvalue"/></root>}

test dom-13.6 {appendFromScript - textnode, commentnode, cdatanode, pinode} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    namespace eval nodeCmds {
        $root appendFromScript {
            t foo
            c "my comment"
            cdata {&"<>;} ;# emacs: "
            pi mypi "some pi data"
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root>foo<!--my comment--><![CDATA[&"<>;]]><?mypi some pi data?></root>}

# emacs: "

test dom-13.7 {appendFromScript - textnode} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    namespace eval nodeCmds {
        $root appendFromScript {
            t "<p>Some <b>important</b> stuff</p>"
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root>&lt;p&gt;Some &lt;b&gt;important&lt;/b&gt; stuff&lt;/p&gt;</root>}

test dom-13.8 {appendFromScript - textnode with -disableOutputEscaping} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    namespace eval nodeCmds {
        $root appendFromScript {
            t -disableOutputEscaping "<p>Some <b>important</b> stuff</p>"
        }
    }
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><p>Some <b>important</b> stuff</p></root>}


test dom-14.1 {appendFromList} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    set errMsg ""
    set result [catch {$root appendFromList {a b}} errMsg] 
    lappend result $errMsg
    $doc delete
    set result
} {1 {invalid element node list format!}} 

test dom-14.2 {appendFromList} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    set errMsg ""
    set result [catch {$root appendFromList {a b c}} errMsg] 
    lappend result $errMsg
    $doc delete
    set result
} {1 {invalid attributes list format!}} 

test dom-14.3 {appendFromList} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    $root appendFromList {a {} {}}
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><a/></root>} 

test dom-14.4 {appendFromList} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    $root appendFromList {#text "foo bar"}
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root>foo bar</root>} 

test dom-14.5 {appendFromList} {
    set doc [dom parse {<elm><foobar/>text<barfoo/><foobaz><a/>text<b/><a/><c/></foobaz></elm>}]
    set elm [$doc documentElement]
    set elmList [$elm asList]
    $doc delete
    set doc [dom createDocument root]
    set root [$doc documentElement]
    $root appendFromList $elmList
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><elm><foobar/>text<barfoo/><foobaz><a/>text<b/><a/><c/></foobaz></elm></root>} 

set doc [dom parse {<elem><!-- comment -->text<child a="v"><!--comment--></child></elem>}]
set docElem [$doc documentElement]

test dom-14.6 {asList of tree with comment nodes} {
    $docElem asList
} {elem {} {{#comment { comment }} {#text text} {child {a v} {{#comment comment}}}}}

test dom-14.7 {asList on a comment} {
    set commentNode [$docElem firstChild]
    $commentNode asList
} {#comment { comment }}

test dom-14.8 {asList on a comment} {
    set commentNode [$docElem selectNodes {(//comment())[2]}]
    $commentNode asList
} {#comment comment}

test dom-14.9 {appendFromList with comment nodes in the list} {
    set list [$docElem asList]
    set newDoc [dom createDocument newDoc]
    set newDocRoot [$newDoc documentElement]
    $newDocRoot appendFromList $list
    set result [$newDoc asXML -indent none]
    $newDoc delete
    set result
} {<newDoc><elem><!-- comment -->text<child a="v"><!--comment--></child></elem></newDoc>}

$doc delete

set doc [dom parse {<elem><?myPI value?>text<child a="v"><?myPI1 the value?></child></elem>}]
set docElem [$doc documentElement]

test dom->14.10 {asList of tree with PI nodes} {
    $docElem asList
} {elem {} {{#pi myPI value} {#text text} {child {a v} {{#pi myPI1 {the value}}}}}}

test dom->14.11 {asList on a PI} {
    set piNode [$docElem firstChild]
    $piNode asList
} {#pi myPI value}

test dom->14.12 {asList on a PI} {
    set piNode [$docElem selectNodes {(//processing-instruction())[2]}]
    $piNode asList
} {#pi myPI1 {the value}}

test dom-14.13 {appendFromList with comment node in the list} {
    set list [$docElem asList]
    set newDoc [dom createDocument newDoc]
    set newDocRoot [$newDoc documentElement]
    $newDocRoot appendFromList $list
    set result [$newDoc asXML -indent none]
    $newDoc delete
    set result
} {<newDoc><elem><?myPI value?>text<child a="v"><?myPI1 the value?></child></elem></newDoc>}

$doc delete

set xml {
<root>
  text node <b>text</b><!-- comment1 -->
  <elem1>text<?pi data?><empty/>
     <child>text</child>
 </elem1><!-- comment2 --><?pi data?>text
</root>}

test dom-15.1 {delete - text nodes} {
    set doc [dom parse $xml]
    set root [$doc documentElement]
    foreach node [$root selectNodes text()] {
        $node delete
    }
    set result [llength [$root childNodes]]
    $doc delete
    set result
} {5}

test dom-15.2 {delete - comment nodes} {
    set doc [dom parse -keepEmpties $xml]
    set root [$doc documentElement]
    foreach node [$root selectNodes comment()] {
        $node delete
    }
    set result [llength [$root childNodes]]
    $doc delete
    set result
} {6}

test dom-15.3 {delete - pi nodes} {
    set doc [dom parse -keepEmpties $xml]
    set root [$doc documentElement]
    foreach node [$root selectNodes processing-instruction()] {
        $node delete
    }
    set result [llength [$root childNodes]]
    $doc delete
    set result
} {7}

test dom-15.4 {delete - pi nodes} {
    set doc [dom parse -keepEmpties $xml]
    set root [$doc documentElement]
    foreach node [$root selectNodes node()] {
        $node delete
    }
    set result [llength [$root childNodes]]
    $doc delete
    set result
} {0}

set doc [dom parse {<root attr1="bingbaz" attr2="ab &amp; zu" attr3=""/>}]
set root [$doc documentElement]

test dom-16.1 {getAttribute} {
    $root getAttribute attr1
} {bingbaz}

test dom-16.2 {getAttribute} {
    $root getAttribute attr2
} {ab & zu}

test dom-16.3 {getAttribute} {
    $root getAttribute attr3
} {}

test dom-16.4 {getAttribute with default} {
    $root getAttribute attr1 "default not needed, because attr1 exists"
} {bingbaz}

test dom-16.5 {getAttribute with default} {
    $root getAttribute notPresent "expect this given default value"
} {expect this given default value}

test dom-16.6 {getAttribute - attr dosen't exists and no default} {
    catch {$root getAttribute notPresent}
} {1}

test dom-16.7 {getAttribute shortcut} {
    $root @attr1
} {bingbaz}

test dom-16.8 {getAttribute shortcut} {
    $root @attr2
} {ab & zu}

test dom-16.9 {getAttribute shortcut} {
    $root @attr3
} {}

test dom-16.10 {getAttribute shortcut with default} {
    $root @attr1 "default not needed, because attr1 exists"
} {bingbaz}

test dom-16.11 {getAttribute shortcut with default} {
    $root @notPresent "expect this given default value"
} {expect this given default value}

test dom-16.12 {getAttribute shortcut - attr dosen't exists and no default} {
    catch {$root @notPresent}
} {1}

$doc delete

# Yea, it's the same string as above. I just love to have the
# data near by the tests, to reduce confusion and silly errors
set xml {
<root>
  text node <b>text</b><!-- comment1 -->
  <elem1>text<?pi data?><empty/>
     <child>text</child>
 </elem1><!-- comment2 --><?pi data?>text
</root>}

set doc [dom parse -keepEmpties $xml]
set root [$doc documentElement]

test dom-17.1 {nodeType} {
    unset result
    foreach node [$root childNodes] {
        lappend result [$node nodeType]
    }
    set result
} {TEXT_NODE ELEMENT_NODE COMMENT_NODE TEXT_NODE ELEMENT_NODE COMMENT_NODE PROCESSING_INSTRUCTION_NODE TEXT_NODE}

test dom-17.2 {nodeType} {
    set CDATAnode [$doc createCDATASection "a CDATA section"]
    $root insertBefore $CDATAnode [$root firstChild]
    [$root firstChild] nodeType
} {CDATA_SECTION_NODE}

$doc delete

set doc [dom parse {<root attr1="bingbaz"
                          attr2="ab &amp; zu"
                          attr3=""
                          foo:attr1="ns attr"
                          xmlns:foo="http://tdom.org/ns"
                          worble2="second attr with 2 in it's name">
text child</root>}]
set root [$doc documentElement]

test dom-18.1 {attributes} {
    $root attributes
} {{foo foo {}} attr1 attr2 attr3 {attr1 foo http://tdom.org/ns} worble2}

test dom-18.2 {attributes} {
    $root attributes *
} {{foo foo {}} attr1 attr2 attr3 {attr1 foo http://tdom.org/ns} worble2}

test dom-18.3 {attributes} {
    $root attributes attr*
} {attr1 attr2 attr3}

test dom-18.4 {attributes} {
    $root attributes *2*
} {attr2 worble2}

test dom-18.5 {attributes} {
    $root attributes worble2
} {worble2}

test dom-18.6 {attributes} {
    $root attributes *brab*
} {}

test dom-18.7 {attributes} {
    [$root firstChild] attributes
} {}

# Hmmm. This two following tests are mostly there to document the
# behavior of the method, as it is.  It may debatable if they should
# behave this way. The optional attribute name pattern is a tDOM
# DOM extension there is nothing in the rec, which could help to argue.
# Therefore, it's the way, it is.

test dom-18.7 {attributes} {
    $root attributes *tdom*
} {}

test dom-18.8 {attributes} {
    $root attributes foo*
} {{attr1 foo http://tdom.org/ns}}

# still the doc from befor 18.1
test dom-19.1 {removeAttribute} {
    $root removeAttribute attr1
    $root attributes attr1
} {}

test dom-19.2 {removeAttribute} {
    catch {$root removeAttribute attr1} errMsg
    set errMsg
} {can't remove attribute 'attr1'}

test dom-19.3 {removeAttribute} {
    catch {$root removeAttribute} 
} {1}

test dom-19.4 {removeAttribute} {
    catch {$root removeAttribute attr2 attr3}
} {1}

test dom-19.5 {removeAttributeNS} {
    $root removeAttributeNS http://tdom.org/ns attr1
    $root hasAttributeNS http://tdom.org/ns attr1
} {0}

test dom-19.6 {removeAttributeNS} {
    catch {$root removeAttributeNS http://tdom.org attr1}
} {1}

$doc delete

set doc [dom parse <root><firstL><secondL1/><secondL2/></firstL></root>]
set root [$doc documentElement]

test dom-20.1 {parentNode} {
    $root parentNode
} {}

test dom-20.2 {parentNode} {
    $root parentNode var
    set var
} {}

test dom-20.3 {parentNode} {
    set child [$root firstChild]
    [$child parentNode] nodeName
} {root}

test dom-20.4 {parentNode} {
    set child [$root firstChild]
    $root removeChild $child
    $child parentNode
} {}

$doc delete

set doc [dom parse {<root><c/><c><cc/></c><c>text</c></root>}]
set root [$doc documentElement]

test dom-21.1 {hasChildNodes} {
    $root hasChildNodes
} {1}

test dom-21.2 {hasChildNodes} {
    set node [$root firstChild]
    $node hasChildNodes
} {0}

test dom-21.3 {hasChildNodes} {
    set node [$root lastChild]
    $node hasChildNodes
} {1}

$doc delete

set doc [dom parse {
<root><p1:e1 xmlns:p1="http://tdom.org" xmlns="http://tdom.org"><e2/></p1:e1>
text</root>}]
set root [$doc documentElement]

test dom-22.1 {localName} {
    $root localName
} {}

test dom-22.2 {localName} {
    [$root firstChild] localName
} {e1}

test dom-22.3 {localName} {
    set node [$root firstChild]
    [$node firstChild] localName
} {e2}

test dom-22.4 {localName} {
    catch {[$root lastChild] localName} errMsg
    set errMsg
} {}

test dom-22.5 {prefix} {
    [$root firstChild] prefix
} {p1}

test dom-22.6 {prefix} {
    set node [$root firstChild]
    [$node firstChild] prefix
} {}

test dom-22.7 {prefix} {
    $root prefix
} {}

$doc delete

test dom-23.1 {replaceChild} {
    set doc [dom parse {<root><e1/><e2>text</e2></root>}]
    set root [$doc documentElement]
    set removedNode [$root removeChild [$root firstChild]]
    $root replaceChild $removedNode [$root firstChild]
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1/></root>}

test dom-24.1 {getLine} {
    set doc [dom parse <root/>]
    set root [$doc documentElement]
    catch {$root getLine}
} {1}

dom setStoreLineColumn 1
set doc [dom parse {<root>
<e1><e2/></e1></root>}]
set root [$doc documentElement]
dom setStoreLineColumn 0

test dom-24.2 {getLine} {
    $root getLine
} {1}

test dom-24.3 {getColumn} {
    $root getColumn
} {0}

test dom-24.4 {getLine} {
    [$root firstChild] getLine
} {2}

test dom-24.5 {getLine getColumn} {
    set node [$root selectNodes //e2]
    set result [$node getLine].[$node getColumn]
} {2.4}

$doc delete

set doc [dom parse {<root attr1="bingbaz"
                          attr2="ab &amp; zu"
                          attr3=""
                          foo:attr1="ns attr"
                          xmlns:foo="http://tdom.org/ns"
                          worble2="second attr with 2 in it's name">
text child</root>}]
set root [$doc documentElement]

test dom-25.1 {hasAttribute} {
    $root hasAttribute attr3
} 1

test dom-25.2 {hasAttribute} {
    $root hasAttribute attr4
} 0

test dom-25.3 {hasAttributeNS} {
    $root hasAttributeNS http://tdom.org/ns attr1
} 1

test dom-25.4 {hasAttributeNS} {
    $root hasAttributeNS http://tdom.org attr1
} 0

$doc delete

test dom-26.1 {appendXML} {
    set doc [dom createDocument root]
    set root [$doc documentElement]
    $root appendXML "<e1>text<e/></e1>"
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1>text<e/></e1></root>}

test dom-26.2 {appendXML} {
    set doc [dom parse <root><e1/></root>]
    set root [$doc documentElement]
    $root appendXML "<e1>text<e/></e1>"
    set result [$root asXML -indent none]
    $doc delete
    set result
} {<root><e1/><e1>text<e/></e1></root>}

set doc [dom parse {<?piBeforeRoot do this?><root>
<?pi1 my &amp; data?><?pi2 data?></root>}]
set root [$doc documentElement]

test dom-27.1 {target, data} {
    set piNode [$root selectNodes /processing-instruction()]
    set result [$piNode target].[$piNode data]
} {piBeforeRoot.do this}
    
test dom-27.2 {target, data} {
    set piNodes [$root selectNodes processing-instruction()]
    set result ""
    foreach piNode $piNodes {
        set result "$result [$piNode target].[$piNode data]"
    }
    set result
} { pi1.my &amp; data pi2.data}

$doc delete

test dom-28.1 {getAttributeNS} {
    set doc [dom parse {
<root attr1="attr1Value"
      attr2="attr2Value"
      p:attr1="p:attr1Value"
      xmlns:p="ns1"/>}]
    set root [$doc documentElement]
    set result [$root getAttributeNS "ns1" attr1]
    $doc delete
    set result
} {p:attr1Value}

proc domAppendChild {parent name} {
    $parent ownerDocument doc
    $doc createElement $name node
    $parent appendChild $node
}

test dom-29.1 {ownerDocument} {
    dom createDocument document doc
    set root [$doc documentElement]
    domAppendChild $root foo1
    domAppendChild $root foo2
    set result [$doc asXML -indent none]
    $doc delete
    set result
} {<document><foo1/><foo2/></document>}


test dom-999.1 {move nodes from one doc to another} {
    set doc1 [dom parse {<root/>}]
    set doc2 [dom parse {<root><e>text</e></root>}]
    set root1 [$doc1 documentElement]
    set root2 [$doc2 documentElement]
    $root1 appendChild [$root2 firstChild]
    set textNode [[$root1 firstChild] firstChild]
    if {[$textNode ownerDocument] == $doc1} {set result 1} else {set result 0}
    $doc1 delete
    $doc2 delete
    set result
} {1}

test dom-999.2 {node references should not change even after renumbering} {
    dom parse {<test><child name="a"/><child name="b"/></test>} doc
    set root [$doc documentElement]
    set oldNode [lindex [$root childNodes] 1]
    dom parse {<NewChild/>} tempDoc 
    set tempNode [$tempDoc documentElement]
    $oldNode appendChild $tempNode
    set childList1 [$root childNodes]
    $root selectNodes {//child[@name='a']}
    set childList2 [$root childNodes]
    if {$childList1 == $childList2} {
        set result 1
    } else {
        set result 0
    }
    $doc delete
    $tempDoc delete
    set result
} {1}

# cleanup
::tcltest::cleanupTests
return
